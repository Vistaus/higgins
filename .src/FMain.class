' Gambas class file

Public locatep As Process
Public results_file As String = Temp()
Public results_list As New String[]
Public rSize As Integer


Public Sub resize_form()
  FMain.h = TextBox1.h + (Desktop.scale * 2)
End

Public Sub TextBox1_Change()
  Dim previous_search As String
  Dim current_search As String
  Try current_search = TextBox1.text 
  If Error Then Return
  
  If current_search = "" Then 
    resize_form()
    Return
  Endif
  
  Try previous_search = TextBox1.text
  If Error Then Return
  Wait 0.5

  Try current_search = TextBox1.text 
  If Error Then Return
  If previous_search <> current_search Then Return
  If String.Len(current_search) < 4 Then Return
  search(current_search)
End

Public Sub initgrid()
  ResultGrid.Rows.count = 0
  ResultGrid.columns.count = 1
  ResultGrid.mode = Select.Single
  ResultGrid.Show()
End

Public Sub reinit()
  results_list.Clear()
  initgrid()
  MyDesktop.init()
  Try locatep.kill
  need_concat = False
  resize_form()
  rSize = ResultGrid.Rows.H
End


Public Sub search(term As String)
  Dim results As String
  reinit()
  FMain.h = TextBox1.h + (Desktop.scale * 2) + resultgrid.h
  FMain.mouse = Mouse.wait
  resultgrid.tag = ""
  Select Left(term)
    Case "="
      resultgrid.tag = "calculate"
      Calculate(Right(term, -1))
    Case Else
      resultgrid.tag = "locate"
      locate(term)
  End Select
  FMain.mouse = Mouse.Default
End

Public Sub Calculate(term As String)
  resultgrid.Rows.count = 1
  Try results_list.Add(Eval(term))
  'Try resultgrid[resultgrid.Rows.max, 0].text = Eval(term)
End


Public Sub locate(term As String)
  locatep = Shell "unbuffer locate -l 1000 -i " & "'" & term & "'" For Read Write As "locatep"
  Debug "locate " & "'" & term & "'" 
  While locatep.state = Process.running
    If closing Then Return
    Wait 0.1
  Wend
End


Private need_concat As Boolean = False

Public Sub locatep_read()
  Dim results, result As String
  Try Read #locatep, results, -4096
  If Left(results) = "\n" Then need_concat = False
  'Stop
  If Error Then Return
    For Each result In Split(results, "\n", "", True)
    If need_concat Then
      results_list[results_list.max] &= result
      need_concat = False
        Else
      ResultGrid.Rows.count += 1
      If resultgrid.Rows.count = 1 Then resultgrid.Rows[0].Selected = True
      If result Ends ".desktop" Then 
        results_list.Add(result, 0)
          Else
        results_list.Add(result)
      Endif
      
    Endif
  Next
  need_concat = Right(results) <> "\n"
End


Public Sub ResultGrid_Data(Row As Integer, Column As Integer)
  Dim img As Image
  Dim t As String
  If results_list.count = 0 Then Return
  t = results_list[Row]
  resultgrid.data.text = t
  Select resultgrid.Tag
    Case "locate"
      If t Ends ".desktop" Then
        MyDesktop.parse(t)
        Try img = MyDesktop.GetIcon(MyDesktop.dIconName, rSize)
      Endif
      If Not img Then Try img = DesktopMime.FromFile(t).GetIcon(rSize)
      If Not Error Then resultgrid.Data.picture = img.picture
    Case "calculate"
      resultgrid.Data.picture = Picture["icon:/small/calculator"]
  End Select
End

Public Sub TextBox1_KeyPress()
  Select Key.Code
    Case Key.down
      ResultGrid.SetFocus()
      Try resultgrid.Rows[1].Selected = True
    Case Key.PgDown
      ResultGrid.SetFocus()
      Try resultgrid.Rows[1].Selected = True
    Case Key.enter
      Try start_item(ResultGrid[0, 0].Text)
      TimerClose.Trigger()
    Case Key.Return
      Try start_item(ResultGrid[0, 0].Text)
      TimerClose.Trigger()
  End Select

End

Public Sub start_item(i As String)
  If i Ends ".desktop" Then
    MyDesktop.init()
    MyDesktop.parse(i)
    If MyDesktop.dExec <> "" Then
      Shell MyDesktop.dExec
      Return
    Endif
  Endif
  
  Desktop.Open(resultgrid.Current.text)
End


Public Sub ResultGrid_activate()
  start_item(resultgrid.Current.text)
  TimerClose.Trigger
End

Public Sub ResultGrid_KeyPress()
  Select Key.Code
    Case Key.Enter
      start_item(resultgrid.Current.text)
      TimerClose.trigger
    Case Key.Return
      start_item(resultgrid.Current.text)
      TimerClose.trigger
    Case Key.up
      If ResultGrid.Rows[0].Selected Then TextBox1.SetFocus()
    Case Key.PageUp
      If ResultGrid.Rows[0].Selected Then TextBox1.SetFocus()
  End Select
End


Public Sub MenuOpenFolder_Click()
Stop
  Try Desktop.Open(File.Dir(resultgrid.Current.text))
End

Public Sub MenuOpenFile_Click()
  Try Desktop.Open(resultgrid.Current.text)
End

Public Sub MenuCopy_Click()
  Try Clipboard.Copy(ResultGrid.Current.Text)
End


Private Function Screenbypos(x As Integer, y As Integer) As Screen
  Dim s As Screen
  For Each s In Screens
    If (x >= s.x) And x <= (s.x + s.W) Then
      If (y >= s.y) And y <= (s.y + s.h) Then
        Return s
      Endif
    Endif
  Next
  
  'no screen found !? repeat with more tolerance:
  For Each s In Screens
    If (x + 50 >= s.x) And x <= (s.x + s.W + 50) Then
      If (y + 50 >= s.y) And y <= (s.y + s.h + 50) Then
        Return s
      Endif
    Endif
  Next

  'still no screen found? return the first.
  Debug "Couldn 't get the matching screen (!)"
  Return Screens[0]
End


Public Function Relative_window_geom(o As Window) As Integer[]
  'get the object position relative to the screen is on
  Dim rx, ry, ox, oy As Integer
  Dim cs As Screen
  Dim out As New Integer[]
  cs = Screenbypos(o.x, o.y)
  ox = cs.X 'offset
  oy = cs.Y 'offset
  rx = o.x - ox
  ry = o.y - oy
  out.Add(rx)
  out.Add(ry)
  out.Add(o.w)
  out.Add(o.h)
  Return out
End

Public Sub Form_Hide()
  fmain.close()
End

Public Sub Form_Open()
  load_geometry()
  reinit()
End

Public Sub load_geometry()
  Dim s As Screen
  Dim rx, ry, w, h As Integer
  s = Screenbypos(Mouse.ScreenX, Mouse.ScreenY)
  rx = Settings["win_rx", 0] 
  ry = Settings["win_ry", 0] 
  w = Settings["win_w", (s.w Div 2)] 
  h = Settings["win_h", 64] 
  fmain.Move(rx + s.x, ry + s.y, w, h)
End

Public Sub save_geometry()
  Dim xy_r As Integer[]
  xy_r = Relative_window_geom(fmain)
  Settings["win_rx"] = xy_r[0]
  Settings["win_ry"] = xy_r[1]
  Settings["win_w"] = xy_r[2]
  Settings["win_h"] = xy_r[3]
End

Private closing As Boolean = False
Public Sub TimerClose_Timer()
  closing = True
  save_geometry()
  fmain.close
End

Public Sub Form_Close()
  TimerClose_Timer()
End

Public Sub Form_KeyRelease()
  If Key.code = Key.esc Then TimerClose.trigger
End

