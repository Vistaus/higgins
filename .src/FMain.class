' Gambas class file

Public xb As XbindKeys
Public Const appname As String = "higgins"
Public Confpath As String = Settings.defaultdir & "/" & appname

Public Struct rItemT
  plugin As String            'the plugin name
  Text As String              'text displayed in the clipboard
  SubText As String           'text displayed under the main text
  hApp As DesktopApps         '.desktop files
  action_desktopopen As String   'the data passed to action desktop.open()
  action_openfolder As String 'the data passed to the action openfolder
  action_activate As String   'the data passed to the action activate
  action_clipboard As String  'the text copied to the clipboard
  image_file As String        'optional image displayed (fullpath or "DefferredMimeIcon")
End Struct


Public results_file As String = Temp()
Public results_list As New RItemT[]
Public picture_list As New Collection
Public rSize As Integer
Public Socket_File As String = Confpath & "/socket"
Public Lock_File As String = Confpath & "/lock"
Public firstrun As Boolean = True

Public Sub resize_form()
  FMain.h = TextBox1.h + (Desktop.scale * 3)
End


Public Sub TextBox1_Change()
  Dim previous_search As String
  Dim current_search As String
  Try current_search = TextBox1.text 
  If Error Then Return
  Wait 0.01
  If current_search = "" Then 
    resize_form()
    reinit()
    Return
  Endif

  Try previous_search = TextBox1.text
  If Error Then Return

  Wait 0.3
  If previous_search <> textbox1.text Then Return

  immediate_search(textbox1)
  If resultgrid.current = Null Then Try resultgrid.Rows[0].Selected = True
  Wait 0.3

  Try current_search = TextBox1.text 
  If Error Then Return
  If previous_search <> current_search Then Return

  returnlabel:
  If resultgrid.current = Null Then Try resultgrid.Rows[0].Selected = True
End


Public Sub initgrid()
  ResultGrid.Rows.count = 0
  ResultGrid.columns.count = 1
  ResultGrid.mode = Select.Single
  ResultGrid.Show()
End

Public Sub reinit()
  results_list.Clear()
  picture_list.Clear()
  initgrid()
  MyDesktop.init()
  mouse_normal()
End


Private plugins As New Collection 'a collection of plugin instances
'Private plugins_enabled As New String[]

Public Sub load_plugin(plugin_name As String, iconsize As Integer)
  Dim myplugin As Object
  myplugin = Object.New(plugin_name, [plugin_name, iconsize])
  Debug plugin_name
  plugins[plugin_name] = myplugin
  Object.Attach(myplugin, fmain, "plugin_event")
End


Public Sub unload_plugin(plugin_name As String)
  Dim my_plugin As Object
  Try my_plugin = plugins[plugin_name]
  If Error Then Return
  
  plugins.Remove(plugin_name)
  Try Object.Call(my_plugin, "stop")
  my_plugin = Null
End


Private previous_search As String
Public Sub immediate_search(textcontrol As Object)
  Dim aPlugin As Object
  Dim t0 As Float

  If previous_search = textcontrol.text Then Return 'don't know why this is important, but works.
  previous_search = textcontrol.text                'don't know why this is important, but works.

  reinit()
  FMain.h = TextBox1.h + (Desktop.scale * 3) + resultgrid.h
  mouse_busy()

  Debug "starting search..."
  For Each aplugin In plugins
    t0 = Timer()
    Debug "[START]", plugins.key
    run_plugin(plugins.key, textbox1)
    Debug "[END]", plugins.key, "time:", Timer() - t0, "[END]"
  Next
  Debug "Search ends here."

  ' run_plugin("plugin_calculator", textbox1)
  ' 
  ' run_plugin("plugin_executable", textbox1)
  ' 
  ' run_plugin("plugin_fbookmarks", textbox1)
  ' 
  ' run_plugin("plugin_apps", textbox1)
  ' 
  ' run_plugin("plugin_ls", textbox1)
  ' 
  ' run_plugin("plugin_indexer", textbox1)

  mouse_normal()
End


Public Sub run_plugin(plugin_name As String, textcontrol As Object)
  Dim rResults As RItemT[]
  Dim rResult As RItemT[]

  rResults = Object.Call(plugins[plugin_name], "find", [textcontrol]) 'chiama il metodo find()
  If rResults = Null Then Return

  results_list.Insert(rResults)
  ResultGrid.Rows.count += rResults.Count

End

Public Sub plugin_event_new_results(rResults As RItemT[])
  results_list.Insert(rResults)
  ResultGrid.Rows.count += rResults.Count
End


Public Sub ResultGrid_Data(Row As Integer, Column As Integer)
  Dim rcResult As New RItemT
  Dim img As Image
  If results_list.count = 0 Then Return
  rcResult = results_list[Row]

  'two lines:
  resultgrid.data.RichText = "<b>" & rcResult.text & "</b>"
  If rcResult.subtext <> "" Then 
    resultgrid.data.RichText &= "<br><font size=-1>" & rcResult.subtext & "</font>"
  Endif

  'find pic from cache?
  If picture_list[row] <> Null Then
    resultgrid.Data.Picture = picture_list[row] 
    Return
  Endif

  'no cache :(
    If rcResult.image_file Like "mimetype://*" Then 
      'need a mimetype cache here or is slow
      Try resultgrid.Data.Picture = MyDesktop.Get_mime_Icon(Right(rcResult.image_file, -11), rSize)
    Endif
    
    If rcResult.image_file Like "resize://*" Then 
      'Stop
      'need a mimetype cache here or is slow
      Try img = Image.load(Right(rcResult.image_file, -9))
      If Not Error Then
        If img.w <> rSize Then img = img.stretch(rSize, rSize)
        resultgrid.Data.Picture = img.picture
      Endif
    Endif
    
    If (resultgrid.Data.Picture = Null) Then 
      Try resultgrid.Data.Picture = Picture[rcResult.image_file]
    Endif
    If resultgrid.Data.Picture = Null Then Try resultgrid.Data.Picture = rcResult.hApp.GetIcon(rSize).Picture

  If resultgrid.Data.picture = Null Then
    resultgrid.Data.Picture = Picture["icon:/" & rSize & "/file"]
  Endif

  'cache picture
  If resultgrid.Data.picture <> Null Then picture_list[row] = resultgrid.Data.picture

End




Public Sub mouse_busy()
  fmain.mouse = Mouse.Wait
  TextBox1.mouse = Mouse.wait
End

Public Sub mouse_normal()
  fmain.mouse = Mouse.Default
  TextBox1.mouse = Mouse.default
End


Public Sub start_item(i As Integer)
  Dim rcResult As RItemT
  Dim msgout As Integer = -1
  If results_list.count = 0 Then Return
  rcResult = results_list[i]
  If is_executable(rcResult.action_activate) Then
    msgout = Message.Question(("Execute file?"), "Execute", "Open")
    Select msgout
      Case 1
        Shell rcResult.action_activate
      Case 2
        Desktop.Open(rcResult.action_desktopopen)
    End Select
  Endif

End

Public Function is_executable(fPath As String) As Boolean
  Dim bout As String
  Dim singlecommand As Boolean = False
  Dim splitted As String[]
  Dim exe As String

  splitted = Split(fPath, ";| &\n", "", True)
  singlecommand = splitted.count = 1
  If Not singlecommand Then singlecommand = singlecommand Or ((fpath Begins "\"") And (fpath Ends "\""))
  If Not singlecommand Then singlecommand = ((fpath Begins "'") And (fpath Ends "'"))

  If singlecommand Then 
    exe = fPath 
      Else 
    exe = splitted[0]
  Endif

  'has to exist
    If Not Exist(exe) Then Return False
  'has to be a file
    If Stat(exe, True).type <> gb.File Then Return False
  'needs executable permissions
    Shell "bash -c \"[[ -x '" & exe & "' ]] && echo executable \"" To bout
    If (Trim(bout) = "") Then Return False
  'needs to have the right mimetype
    If Not MyDesktop.mime_exe(exe) Then Return False
  'right then!
    Return True
End





Public Sub ResultGrid_activate()
  start_item(resultgrid.row)
  TimerClose.Trigger
End

Public Sub TextBox1_KeyPress()
  Select Key.Code
    
    Case Key.down
      ResultGrid.SetFocus()
      If resultgrid.current = Null Then 
        Try resultgrid.Rows[0].Selected = True
          Else
        Try resultgrid.Rows[1].Selected = True
      Endif
      
    Case Key.PgDown
      ResultGrid.SetFocus()
      If resultgrid.current = Null Then 
        Try resultgrid.Rows[0].Selected = True
          Else
        Try resultgrid.Rows[1].Selected = True
      Endif
      
    Case Key.enter
      Try start_item(0)
      If Not Error Then timerClose.Trigger()
      
    Case Key.Return
      Try start_item(0)
      If Not Error Then timerClose.Trigger()
      
  End Select
End

Public Sub ResultGrid_KeyPress()
  Select Key.Code
    Case Key.Enter
      start_item(resultgrid.row)
      TimerClose.trigger
    Case Key.Return
      start_item(resultgrid.row)
      TimerClose.trigger
    Case Key.up
      If ResultGrid.Rows[0].Selected Then TextBox1.SetFocus()
    Case Key.PageUp
      If ResultGrid.Rows[0].Selected Then TextBox1.SetFocus()
  End Select
End


Public Sub MenuOpenFolder_Click()
  Try Desktop.Open(results_list[resultgrid.row].action_openfolder)
  TimerClose.Trigger
End


Public Sub MenuOpenFile_Click()
  start_item(resultgrid.row)
  TimerClose.Trigger
End


Public Sub MenuCopy_Click()
  Clipboard.Copy(results_list[ResultGrid.row].action_clipboard)
End

'======================================== START SOCKET FUNCTIONS ========================================

    Public My_listening_socket As ServerSocket
    
    
    Public hLock As Stream
    
    Public Sub socket_init()
      'TRY TO get a LOCK file:
      Try hLock = Lock Lock_File
      If Error Then 'alreadyrunning
        Debug "another instance already running"
        show_other_instance()
        Quit
      Endif
      'start to listen for remote commands, one connection is ok.
      My_listening_socket = New ServerSocket As "My_listening_socket"
      My_listening_socket.type = Net.Unix
      My_listening_socket.path = Socket_File
      My_listening_socket.Listen(1)
    End
    
    Public MyCSock As Socket
    Public Sub show_other_instance()
      Dim sBuf As String
      MyCSock = New Socket
      MyCSock.path = Socket_File
      MyCSock.Port = Net.Local
      MyCSock.Connect()
      While (MyCSock.Status <> 7) And (MyCSock.Status > 0)
        Wait 0.1
      Wend
      If MyCSock.Status = 7 Then
        sBuf = "show\n"
        Write #MyCSock, sBuf, Len(sBuf)
        Close #MycSock
      Endif
    End
    
    Public Host As Object
    Public Sub My_listening_socket_connection(RemoteHostIP As String)
      'just accept on localhost.
      host = My_listening_socket.Accept()
    End
    
    Public Sub socket_Read()
      Dim command As String = ""
      'Stop
      Line Input #Last, command
      If command = "show" Then
        Repeat
          fmain.visible = True
          Wait
        Until fmain.visible = True
      Endif
      My_listening_socket.close
      My_listening_socket.Listen(1)
    End

'-- END SOCKET FUNCTIONS ---------------------------------------------------------------------

'--- TRYICON RELATED
              
    Public TrayIcon2 As Trayicon
    Public Sub InitTrayIcon()
    
      Dim SystemTray As Boolean = True
      Try SystemTray = desktop.HasSystemTray
      If Error Then
        Debug "Couldn't check if your desktop has a system tray, assuming it has"
      Endif
    
      If Not SystemTray Then Return
      Try TrayIcon2 = New Trayicon 
      If Error Then Debug "Cannot Init Trayicon"
      TrayIcon2.Picture = Picture["appicon.png"]
      TrayIcon2.Visible = True
      object.Attach(TrayIcon2, Me, "TrayIcon2")
      TrayIcon2.PopupMenu = "TrayMenu"
      If Error Then Debug "Cannot Init Trayicon"
    End
    
    Public Sub TrayIcon2_click()
      If fmain.visible Then 
        Form_Hide()
          Else
        Form_Show()
      Endif
    End
    
    Public Sub MenuTrayOptions_Click()
      hSettings.Show()
    End
    
    
    Public Sub QuitMenuTray_Click()
      myQuit()
    End
    
    Public Sub MenuTrayShow_click()
      Form_Show()
    End


'-- END TRAYICON FUNCTIONS ---------------------------------------------------------------------


'-- XBINDKEYS ---------------------------------------------------------------------

    Public Sub xb_HotkeyPressed(action As String)
      If action = "show" Then
        If fmain.visible Then
          fmain.Hide()
            Else
          fmain.Show()
        Endif
      Endif
    End
    
    Public Sub setup_xbindkeys()
      Dim rcpath As String = confpath & "/higgins.xb.rc"
      Shell "killall " & confpath & "/xbindkeys_higgins" Wait
      xb = New XbindKeys(rcpath, confpath & "/xbindkeys_higgins") As "xb"
      If Not Exist(rcpath) Then 
        xb.register("show")
      Endif
    End

'-- END XBINDKEYS ---------------------------------------------------------------------


Public Sub init_hSettings()
  Dim plugin_name As String
  Dim o_hbox As Hbox
  Dim o_checkbox As Checkbox
  Dim o_spacer As Panel
  Dim o_button As Button
  Dim o_separator As Separator
  Dim plist As String 
  Dim plugins_enabled As New String[]
  
  Try plist = Settings["hsettings/plist"]
  plugins_enabled = Split(plist)
  
  For Each plugin_name In Dir(".gambas")
    If (Upper(plugin_name) Begins "PLUGIN_") Then
      If Not (Upper(plugin_name) Ends "_GUI") Then

        o_hbox = New HBox(hSettings.frame1)
        o_hbox.h = hSettings.Font.TextHeight("|") * 1.5

        o_checkbox = New Checkbox(o_hbox) As "plugin_checkbox"
        o_checkbox.text = plugin_name
        o_checkbox.AutoResize = True
        o_checkbox.tag = plugin_name
        
        Object.Lock(o_checkbox)
        If ((plugins_enabled.find(plugin_name) > -1) Or plist = "") Then
          o_checkbox.value = CheckBox.True

          load_plugin(plugin_name, rsize)
            Else
          o_checkbox.value = CheckBox.false
        Endif
        Object.unLock(o_checkbox)

        o_spacer = New Panel(o_hbox)
        o_spacer.border = Border.None
        o_spacer.expand = True

        o_button = New Button(o_hbox) As "plugin_button"
        o_button.tag = plugin_name

        o_button.w = hSettings.Font.TextHeight("|") * 1.5
        o_button.Picture = Picture["icon:/medium/options"]

        o_separator = New Separator(hSettings.frame1)

      Endif
    Endif
  Next
  
End



Public Sub plugin_checkbox_Click()

  If Last.value = CheckBox.True Then
    'If plugins_enabled.Find(Last.tag) = -1 Then
    If Not (plugins.Exist(Lower(Last.tag))) Then
      'plugins_enabled.Add(Last.tag)
      load_plugin(Last.tag, rsize)
    Endif
        Else
    unload_plugin(Last.tag)
    'plugins_enabled.Delete(plugins_enabled.Find(Last.tag))
  Endif

  save_plugin_list()

End

Public Sub plugin_button_Click()
  Dim myplugin As Object
  Try myplugin = plugins[Last.tag]
  If Error Then Return
  Try Object.Call(myplugin, "configure")
  
End


Public Sub save_plugin_list()
  Dim p As New String[]
  Dim o As Object
  For Each o In hSettings.Controls
    If o Is CheckBox Then
      If o.tag Begins "PLUGIN" Then
        If o.value = CheckBox.True Then p.Add(o.tag)
      Endif
    Endif
  Next
  Settings["hsettings/plist"] = p.Join()
  Settings.save
End



Public Sub TimerClose_Timer()
  MySettings.save_geometry()
  fmain.close

End

Public Sub myQuit()
  Try xb.close
  Wait
  TrayIcon2.Delete()
  fmain.Delete()
  fmain.Close()
  Quit
End

'-- FMAIN FUNCTIONS  ---------------------------------------------------------------------

    Public Sub Form_Open()
      Dim padding As Integer = (Desktop.scale * 2)
      Dim newuser As Boolean = False
      Dim msg As String
      Dim i As Integer
      Dim img As Image
      Dim img2 As Image
      Dim t0 As Float

      t0 = Timer()
      If Not firstrun Then Return
      If Not Exist(Confpath, True) Then
        newuser = True
        Mkdir confpath
      Endif
      If Newuser Then
        msg = ("Higgins is not configured") & "\n"
        msg &= ("Please configure at least the hotkey") & "\n"
        msg &= ("to show the higgins window.") & "\n"
        Message.Info(msg)
      Endif
      setup_xbindkeys()
      resultgrid.Rows.h = resultgrid.Font.RichTextHeight("|j<br>|j") + padding
      rSize = ResultGrid.Rows.H - padding
      MySettings.load()
      socket_init()
      InitTrayIcon()
      
      resize_form()
      
      init_hSettings()

    End
    
    Public Sub Form_KeyRelease()
      If Key.code = Key.esc Then Form_Hide()
    End
    
    Public Sub Form_Hide()
      If fmain.tag = "minimized" Then Return
      MySettings.save_geometry()
      fmain.visible = False
      fmain.tag = "minimized"
    '  MySettings.plugin_indexer_last_index_time = indexer.last_index_time
      MySettings.save()
    End
    
    Public Sub Form_Show()
      If firstrun Then 
        fmain.hide
        firstrun = False
        Return
      Endif
      MySettings.load_geometry()
    
      fmain.visible = True
      fmain.tag = ""
      resize_form()
      Wait
      TextBox1.SetFocus()
    
    End
    
    ' Public Sub Form_Close()
    '   Form_Hide()
    ' End

'-- END FMAIN FUNCTIONS  ---------------------------------------------------------------------


Public Sub QuitMenu_Click()
  myQuit()
End





Public Sub TextBox1_GotFocus()
  Dim o As Object
  o = findSelectable(TextBox1)
  Wait
  If o <> Null Then 
    o.selectall
  Endif
End


Private Function findSelectable(o As Object) As Object
  Dim c As Integer = 0
  Dim child, selectable As Object
  Try o.selectall
  If Error Then
    Try c = O.children.count
    If c > 0 Then
      For Each child In O.children
        selectable = findSelectable(CHILD)
        If selectable <> Null Then 
          Return selectable
        Endif
      Next
    Endif
      Else
    Return o
  Endif
End


Public Sub TextBox1_Click()
  Me.hide
  hSettings.show
End

Public Sub indexer_indexing_done(finish_time As Date)
  
  MySettings.plugin_indexer_last_index_time = finish_time
  
End


